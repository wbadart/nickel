use crate::identifier::Ident;
use crate::term::{BinaryOp, RichTerm, Term, UnaryOp, StrChunk};
use crate::types::{Types, AbsType};
use super::utils::{mk_span, mk_label};
use super::lexer::{Token, NormalToken, StringToken, LexicalError};
use std::collections::HashMap;
use either::*;
use codespan::FileId;

grammar<'input>(src_id: FileId);

pub Term: RichTerm = SpTerm<RichTerm>;

SpTerm<Rule>: RichTerm =
    <l: @L> <t: Rule> <r: @R> => match t {
        RichTerm {term: t, pos: _} => RichTerm {
            term: t,
            pos: Some(mk_span(src_id, l, r))
        }
    };

RichTerm: RichTerm = {
    <l: @L> "fun" <ps:Pattern+> "=>" <t: SpTerm<Term>> <r: @R> => {
        let pos = Some(mk_span(src_id, l, r));
        ps.into_iter().rev().fold(t, |t, p| RichTerm {
            term: Box::new(Term::Fun(p, t)),
            pos: pos.clone()
        })
    },
    "let" <id:Ident> "=" <t1:SpTerm<Term>> "in" <t2:SpTerm<Term>> =>
        RichTerm::new(Term::Let(id, t1, t2)),
    "if" <b:SpTerm<Term>> "then" <t:SpTerm<Term>> "else" <e:SpTerm<Term>> =>
        RichTerm::app(RichTerm::app(RichTerm::new(Term::Op1(UnaryOp::Ite(), b)), t), e),
    "import" <s: Str> => RichTerm::new(Term::Import(s)),
    SpTerm<Operation>
};

Operation: RichTerm = {
    <t1: SpTerm< Applicative>> <op: BOpIn> <t2: SpTerm<Operation>> => RichTerm::new(Term::Op2(op, t1, t2)),
    SpTerm< Applicative>,
};

Applicative: RichTerm = {
    <t1:SpTerm< Applicative>> <t2: SpTerm<Atom>> => RichTerm::new(Term::App(t1, t2)),
    <op: UOp> <t: SpTerm<Atom>> => RichTerm::new(Term::Op1(op, t)),
    <op: BOpPre> <t1: SpTerm<Atom>> <t2: SpTerm<Atom>> => RichTerm::new(Term::Op2(op, t1, t2)),
    SpTerm<RecordOperationChain>,
    SpTerm<Atom>,
};

RecordOperand: RichTerm = {
    SpTerm<Atom>,
    SpTerm<RecordOperationChain>,
}

RecordOperationChain: RichTerm = {
    <t: SpTerm<RecordOperand>> "." <id: Ident> => RichTerm::new(Term::Op1(UnaryOp::StaticAccess(id), t)),
    <t: SpTerm<RecordOperand>> ".$" <t_id: SpTerm<Atom>> => RichTerm::new(Term::Op2(BinaryOp::DynAccess(), t_id, t)),
    <t: SpTerm<RecordOperand>> "-$" <t_id: SpTerm<Atom>> => RichTerm::new(Term::Op2(BinaryOp::DynRemove(), t_id, t)),
    <r: SpTerm<RecordOperand>> "$[" <id: SpTerm<Term>> "=" <t: SpTerm<Term>> "]" =>
        RichTerm::new(Term::Op2(BinaryOp::DynExtend(t), id, r)),
};

Atom: RichTerm = {
    "(" <SpTerm<Term>> ")",
    <l: @L> "Promise(" <ty: Types> "," <t: SpTerm<Term>> ")" <r: @R> =>
        RichTerm::new(Term::Promise(ty.clone(), mk_label(ty, src_id, l, r), t)),
    <l: @L> "Assume(" <ty: Types> "," <t: SpTerm<Term>> ")" <r: @R> =>
        RichTerm::new(Term::Assume(ty.clone(), mk_label(ty, src_id, l, r), t)),
    <l: @L> "Contract(" <ty: Types> ")" <r: @R> =>
        RichTerm::new(Term::Contract(ty.clone(), mk_label(ty, src_id, l, r))),
    "Default(" <t: SpTerm<Term>> ")" => RichTerm::new(Term::DefaultValue(t)),
    <l: @L> "ContractDefault(" <ty: Types> "," <t: SpTerm<Term>> ")" <r: @R> =>
        RichTerm::new(Term::ContractWithDefault(ty.clone(),
            mk_label(ty, src_id, l, r), t)
        ),
    "Docstring(" <s: Str> "," <t: SpTerm<Term>> ")" => RichTerm::new(Term::Docstring(s, t)),
    "num literal" => RichTerm::new(Term::Num(<>)),
    Bool => RichTerm::new(Term::Bool(<>)),
    <StrChunks>,
    Ident => RichTerm::new(Term::Var(<>)),
    "`" <Ident> => RichTerm::new(Term::Enum(<>)),
    "{" <fields: (RecordField ";")*> <last: RecordField?> "}" => {
        let mut static_map = HashMap::new();
        let mut dynamic_fields = Vec::new();

        fields
            .into_iter()
            .map(|x| x.0)
            .chain(last.into_iter())
            .for_each(|field| match field {
                Left((id, t)) => { static_map.insert(id, t) ;}
                Right(t) => dynamic_fields.push(t),
            });

        let static_rec = RichTerm::new(Term::RecRecord(static_map));

        dynamic_fields.into_iter().fold(static_rec, |rec, field| {
            let (id_t, t) = field;
            RichTerm::new(Term::Op2(BinaryOp::DynExtend(t), id_t, rec))
        })
    },
    "[" <terms: (SpTerm<Atom> ",")*> <last: SpTerm<Term>?> "]" => {
        let terms : Vec<RichTerm> = terms.into_iter()
            .map(|x| x.0)
            .chain(last.into_iter()).collect();
        RichTerm::new(Term::List(terms))
    }
};

RecordField: Either<(Ident, RichTerm), (RichTerm, RichTerm)> = {
    <id: Ident> "=" <t: SpTerm<Term>> =>
        Either::Left((id, t)),
    "$" <id: SpTerm<Term>> "=" <t: SpTerm<Term>> =>
        Either::Right((id, t)),
}

Pattern: Ident = {
    Ident,
};

Ident: Ident = "identifier" => Ident(<>.to_string());

Bool: bool = {
    "true" => true,
    "false" => false,
};

StrChunks : RichTerm =
  "\"" <fst: ChunkLiteral?> <chunks: (ChunkExpr+ChunkLiteral)*> <lasts:
    ChunkExpr*> "\"" => {
        let chunks: Vec<StrChunk<RichTerm>> = fst.into_iter()
            .map(StrChunk::Literal)
            .chain(chunks.into_iter()
                .map(|(mut es, s)| {
                    es.push(StrChunk::Literal(s));
                    es
                })
                .flatten())
            .chain(lasts.into_iter())
            .rev()
            .collect();

        RichTerm::new(Term::StrChunks(chunks))
    };

ChunkLiteral: String = 
    <parts: ChunkLiteralPart+> => {
        parts.into_iter().fold(String::new(), |mut acc, part| {
            match part {
                Either::Left(s) => acc.push_str(s),
                Either::Right(c) => acc.push(c),
            };

            acc
        })
    };

ChunkExpr: StrChunk<RichTerm> = "${" <t: SpTerm<RichTerm>> "}" => StrChunk::Expr(t);

Str: String = "\"" <s: ChunkLiteral> "\"" => s;

ChunkLiteralPart: Either<&'input str, char> = {
    "str literal" => Either::Left(<>),
    "escaped char" => Either::Right(<>),
};

UOp: UnaryOp<RichTerm> = {
    "isZero" => UnaryOp::IsZero(),
    "isNum" => UnaryOp::IsNum(),
    "isBool" => UnaryOp::IsBool(),
    "isStr" => UnaryOp::IsStr(),
    "isFun" => UnaryOp::IsFun(),
    "isList" => UnaryOp::IsList(),
    "isRecord" => UnaryOp::IsRecord(),
    "blame" => UnaryOp::Blame(),
    "chngPol" => UnaryOp::ChangePolarity(),
    "polarity" => UnaryOp::Pol(),
    "goDom" => UnaryOp::GoDom(),
    "goCodom" => UnaryOp::GoCodom(),
    "tag" <s: Str> => UnaryOp::Tag(s),
    "wrap" => UnaryOp::Wrap(),
    "embed" <Ident> => UnaryOp::Embed(<>),
    "switch" "{" <ds: (switch_case ",")*> <default: switch_default?> "}" =>
        UnaryOp::Switch(
            ds.into_iter().map(|x| x.0).collect(),
            default,
        ),
    "mapRec" <Atom> => UnaryOp::MapRec(<>),
    "seq" => UnaryOp::Seq(),
    "deepSeq" => UnaryOp::DeepSeq(),
    "head" => UnaryOp::ListHead(),
    "tail" => UnaryOp::ListTail(),
    "length" => UnaryOp::ListLength(),
    "fieldsOf" => UnaryOp::FieldsOf(),
};

switch_case: (Ident, RichTerm) = {
    <id: Ident> "=>" <t: SpTerm<Atom> > => (id, t),
}

switch_default: RichTerm = {
    "_" "=>" <SpTerm<Atom>> "," => <>,
}

BOpIn: BinaryOp<RichTerm> = {
    "operator" => {
        match <> {
            "+" => BinaryOp::Plus(),
            "++" => BinaryOp::PlusStr(),
            "==" => BinaryOp::Eq(),
            "@" => BinaryOp::ListConcat(),
            op => panic!("Unkown operator {}", op)
        }
    },
};

BOpPre: BinaryOp<RichTerm> = {
    "unwrap" => BinaryOp::Unwrap(),
    "goField" => BinaryOp::GoField(),
    "hasField" => BinaryOp::HasField(),
    "map" => BinaryOp::ListMap(),
    "elemAt" => BinaryOp::ListElemAt(),
    "merge" => BinaryOp::Merge(),
}

Types: Types = {
    "forall" <ids: Ident+> "." <ty: Arrows> =>
        ids.into_iter().rev().fold(
            ty,
            |acc, id| Types(AbsType::Forall(id, Box::new(acc)))
        ),
    <Arrows>
}

Arrows: Types = {
    <s: subType> "->" <t:Arrows> => Types(AbsType::Arrow(Box::new(s), Box::new(t))),
    <subType>,
};

BaseType: Types = {
    "Dyn" => Types(AbsType::Dyn()),
    "Num" => Types(AbsType::Num()),
    "Bool" => Types(AbsType::Bool()),
    "Str" => Types(AbsType::Str()),
    "List" => Types(AbsType::List()),
};

subType : Types = {
    <BaseType>,
    <Ident> => Types(AbsType::Var(<>)),
    "#" <SpTerm<RichTerm>> => Types(AbsType::Flat(<>)),
    "(" <Types> ")" => <>,
    "(" "|" <r:(<Ident> ",")*> <last: (<Ident>)?>"|" <rest: Ident?> ")" =>
        r.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                Types(
                    match rest {
                        Some(id) => AbsType::Var(id),
                        None => AbsType::RowEmpty(),
                    }
                ),
                |t, i| Types(AbsType::RowExtend(i, None, Box::new(t)))
            ),
    "{" "|" <r:(<Ident> ":" <Types> ",")*> <last:(<Ident> ":" <Types>)?> "|"
    <rest: Ident?> "}" =>
        r.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                Types(
                    match rest {
                        Some(id) => AbsType::Var(id),
                        None => AbsType::RowEmpty(),
                    }
                ),
                |t, i_ty| {
                    let (i, ty) = i_ty;
                    Types(AbsType::RowExtend(i, Some(Box::new(ty)), Box::new(t)))
                }
            ),
    "<" <subType> ">" => Types(AbsType::Enum(Box::new(<>))),
    "{" <subType> "}" => Types(AbsType::StaticRecord(Box::new(<>))),
    "{" "_" ":" <Types> "}" => Types(AbsType::DynRecord(Box::new(<>))),
};

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token<'input> {
        "identifier" => Token::Normal(NormalToken::Identifier(<&'input str>)),
        "operator" => Token::Normal(NormalToken::BinaryOp(<&'input str>)),
        "str literal" => Token::Str(StringToken::Literal(<&'input str>)),
        "escaped char" => Token::Str(StringToken::EscapedChar(<char>)),
        "num literal" => Token::Normal(NormalToken::NumLiteral(<f64>)),

        "if" => Token::Normal(NormalToken::If),
        "then" => Token::Normal(NormalToken::Then),
        "else" => Token::Normal(NormalToken::Else),
        "forall" => Token::Normal(NormalToken::Forall),
        "in" => Token::Normal(NormalToken::In),
        "let" => Token::Normal(NormalToken::Let),
        "switch" => Token::Normal(NormalToken::Switch),

        "true" => Token::Normal(NormalToken::True),
        "false" => Token::Normal(NormalToken::False),

        "," => Token::Normal(NormalToken::Comma),
        ":" => Token::Normal(NormalToken::Colon),
        "$" => Token::Normal(NormalToken::Dollar),
        "=" => Token::Normal(NormalToken::Equals),
        ";" => Token::Normal(NormalToken::SemiCol),
        "." => Token::Normal(NormalToken::Dot),
        ".$" => Token::Normal(NormalToken::DotDollar),
        "$[" => Token::Normal(NormalToken::DollarBracket),
        "${" => Token::Str(StringToken::DollarBrace),
        "-$" => Token::Normal(NormalToken::MinusDollar),
        "$=" => Token::Normal(NormalToken::DollarEquals),
        "fun" => Token::Normal(NormalToken::Fun),
        "import" => Token::Normal(NormalToken::Import),
        "|" => Token::Normal(NormalToken::Pipe),
        "->" => Token::Normal(NormalToken::SimpleArrow),
        "=>" => Token::Normal(NormalToken::DoubleArrow),
        "#" => Token::Normal(NormalToken::Hash),
        "`" => Token::Normal(NormalToken::Backtick),
        "_" => Token::Normal(NormalToken::Underscore),
        "\"" => Token::Normal(NormalToken::DoubleQuote),

        "Num" => Token::Normal(NormalToken::Num),
        "Dyn" => Token::Normal(NormalToken::Dyn),
        "Str" => Token::Normal(NormalToken::Str),
        "Bool" => Token::Normal(NormalToken::Bool),
        "List" => Token::Normal(NormalToken::List),

        "tag" => Token::Normal(NormalToken::Tag),
        "Assume(" => Token::Normal(NormalToken::Assume),
        "Promise(" => Token::Normal(NormalToken::Promise),
        "Default(" => Token::Normal(NormalToken::Deflt),
        "Contract(" => Token::Normal(NormalToken::Contract),
        "ContractDefault(" => Token::Normal(NormalToken::ContractDeflt),
        "Docstring(" => Token::Normal(NormalToken::Docstring),

        "isZero" => Token::Normal(NormalToken::IsZero),
        "isNum" => Token::Normal(NormalToken::IsNum),
        "isBool" => Token::Normal(NormalToken::IsBool),
        "isStr" => Token::Normal(NormalToken::IsStr),
        "isFun" => Token::Normal(NormalToken::IsFun),
        "isList" => Token::Normal(NormalToken::IsList),
        "isRecord" => Token::Normal(NormalToken::IsRecord),
        "blame" => Token::Normal(NormalToken::Blame),
        "chngPol" => Token::Normal(NormalToken::ChangePol),
        "polarity" => Token::Normal(NormalToken::Polarity),
        "goDom" => Token::Normal(NormalToken::GoDom),
        "goCodom" => Token::Normal(NormalToken::GoCodom),
        "goField" => Token::Normal(NormalToken::GoField),
        "wrap" => Token::Normal(NormalToken::Wrap),
        "unwrap" => Token::Normal(NormalToken::Unwrap),
        "embed" => Token::Normal(NormalToken::Embed),
        "mapRec" => Token::Normal(NormalToken::MapRec),
        "seq" => Token::Normal(NormalToken::Seq),
        "deepSeq" => Token::Normal(NormalToken::DeepSeq),
        "head" => Token::Normal(NormalToken::Head),
        "tail" => Token::Normal(NormalToken::Tail),
        "length" => Token::Normal(NormalToken::Length),
        "fieldsOf" => Token::Normal(NormalToken::FieldsOf),

        "hasField" => Token::Normal(NormalToken::HasField),
        "map" => Token::Normal(NormalToken::Map),
        "elemAt" => Token::Normal(NormalToken::ElemAt),
        "merge" => Token::Normal(NormalToken::Merge),

        "{" => Token::Normal(NormalToken::LBrace),
        "}" => Token::Normal(NormalToken::RBrace),
        "[" => Token::Normal(NormalToken::LBracket),
        "]" => Token::Normal(NormalToken::RBracket),
        "(" => Token::Normal(NormalToken::LParen),
        ")" => Token::Normal(NormalToken::RParen),
        "<" => Token::Normal(NormalToken::LAngleBracket),
        ">" => Token::Normal(NormalToken::RAngleBracket),
    }
}
